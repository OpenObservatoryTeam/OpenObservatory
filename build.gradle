plugins {
    id 'jacoco'
    id 'java'
    id 'com.diffplug.spotless' version '6.17.0'
    id 'io.spring.dependency-management' version '1.1.0'
    id 'org.sonarqube' version '4.0.0.2929'
    id 'org.springframework.boot' version '3.0.5'
}

def coverageExclusionList = [
        '**/achievements/**/*',
        '**/configuration/**/*',
        '**/controllers/**/*',
        '**/dto/**/*',
        '**/entities/**/*',
        '**/exceptions/**/*',
        '**/providers/**/*',
        '**/repositories/**/*',
        '**/*Application*'
]

group = 'fr.openobservatory'
sourceCompatibility = '17'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'nl.martijndwars:web-push:5.1.1'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.1.0'
    implementation 'org.bouncycastle:bcprov-jdk15on:1.70'
    implementation 'org.modelmapper:modelmapper:3.1.1'
    implementation 'org.openapitools:jackson-databind-nullable:0.2.6'
    implementation 'org.springframework.boot:spring-boot-starter-cache'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'io.sentry:sentry-spring-boot-starter:6.16.0'
    implementation 'io.sentry:sentry-spring-boot-starter-jakarta:6.16.0'
    implementation 'io.sentry:sentry-logback:6.16.0'
    compileOnly 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    runtimeOnly 'org.postgresql:postgresql'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testCompileOnly 'org.assertj:assertj-core:3.24.2'
}

tasks.named('test') {
    useJUnitPlatform()
}

jacocoTestReport {
    dependsOn test
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, excludes: coverageExclusionList)
        }))
    }
    reports {
        xml.enabled true
    }
}

spotless {
    java {
        googleJavaFormat()
    }
}

sonarqube {
    properties {
        property "sonar.projectName", "Open Observatory - Backend"
        property "sonar.projectKey", "openobservatoryteam_open-observatory-backend"
        property "sonar.organization", "openobservatoryteam"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.coverage.exclusions", coverageExclusionList.join(', ')
    }
}
